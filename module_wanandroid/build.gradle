if (module_wanandroid.toBoolean()) {
    apply plugin: 'com.android.application'
}else {
    apply plugin: 'com.android.library'
}
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-parcelize'
apply plugin: 'kotlin-kapt'
apply plugin: 'dagger.hilt.android.plugin'

def compose_version = '1.0.0'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        if (module_wanandroid.toBoolean()) {
            applicationId "com.benyq.guochat.wanandroid"
        }

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"

        kapt {
            arguments {
                arg("AROUTER_MODULE_NAME", project.getName())
            }
        }

    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion compose_version
        kotlinCompilerVersion '1.5.10'
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
    sourceSets {
        main {
            if (module_wanandroid.toBoolean()) {
                manifest.srcFile 'src/main/moduleManifest/AndroidManifest.xml'
            }else {
                manifest.srcFile 'src/main/AndroidManifest.xml'
            }
        }
    }
}

dependencies {

    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation project(':module_base')

    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.compose.material:material:$compose_version"
    implementation "androidx.compose.ui:ui-tooling-preview:$compose_version"
    implementation "androidx.compose.ui:ui-tooling:1.0.1"
    implementation 'androidx.activity:activity-compose:1.3.1'
    implementation "androidx.compose.runtime:runtime-livedata:1.0.0"
    implementation 'androidx.appcompat:appcompat:1.3.0'
    implementation 'com.google.android.material:material:1.3.0'
    debugImplementation "androidx.compose.ui:ui-tooling:$compose_version"
    implementation 'androidx.lifecycle:lifecycle-viewmodel-compose:1.0.0-alpha07'

    implementation "io.coil-kt:coil-compose:1.3.1"
    implementation "com.google.accompanist:accompanist-coil:0.15.0"

    implementation 'com.github.franmontiel:PersistentCookieJar:v1.0.1'

    implementation "com.google.accompanist:accompanist-systemuicontroller:0.16.0"
    implementation "com.google.accompanist:accompanist-pager:0.16.0"
    implementation "com.google.accompanist:accompanist-pager-indicators:0.16.0"

    kapt "com.alibaba:arouter-compiler:$arouterCompiler"

    implementation "com.google.dagger:hilt-android:$hilt_version"
    implementation "androidx.hilt:hilt-lifecycle-viewmodel:$hilt_lifecircle_version"
    kapt "androidx.hilt:hilt-compiler:$hilt_lifecircle_version"
    kapt "com.google.dagger:hilt-android-compiler:$hilt_version"

    //paging
    implementation "androidx.paging:paging-runtime-ktx:3.0.1"
    implementation "androidx.paging:paging-compose:1.0.0-alpha12"
}